//SPDX-License-Identifier: MIT
/*                                                                                                                                                                                                     
                                                     .                                              
                                                  .+%.                                              
                                                 *@@+                                               
                                               =@@@@-                                               
                                              *@@@@@-                                               
                               .             -@@@@@@+               .                               
                             .+%             %@@@%@@@               :%=                             
                            =@@+            .+=+*#*++.               @@%.                           
                           =@@@+            -%@@@@@@@@+              @@@@.                          
                          :@@@@%           *@@@@@@@@@@@%.           :@@@@%                          
                          #@@@@@=         +@@@@@@@@@@@@@%           #@@@@@-                         
                          ***##*+        .@@@@@@@@@@@@@@@*         .+*#**+=                         
                         -%@@@@@@%-      +@@@@@@@@@@@@@@@@        +@@@@@@@#.                        
             :          =@@@@@@@@@@*     *@@@@@@@@@@@@@@@@:     .%@@@@@@@@@@.        -              
           .*%         :@@@@@@@@@@@@+    *@@@@@@@@@@@@@@@@:    .@@@@@@@@@@@@%        =%-            
          =@@*         #@@@@@@@@@@@@@:   -@@@@@@@@@@@@@@@@     *@@@@@@@@@@@@@:       .@@%:          
         =@@@%         @@@@@@@@@@@@@@#    #@@@@@@@@@@@@@@+    .@@@@@@@@@@@@@@+       .@@@@:         
        .@@@@@:       .@@@@@@@@@@@@@@@    .%@@@@@@@@@@@@#     =@@@@@@@@@@@@@@*       +@@@@@         
        +@@@%%@.      .@@@@@@@@@@@@@@@.     *@@@@@@@@@@+      +@@@@@@@@@@@@@@*      :@%#%@@=        
        ==+*##*=       @@@@@@@@@@@@@@@       :*@@@@@@*:       =@@@@@@@@@@@@@@=      :+###*+-        
        =@@@@@@@@+     =@@@@@@@@@@@@@#          .::.          .@@@@@@@@@@@@@@.    .#@@@@@@@%:       
       *@@@@@@@@@@#.    *@@@@@@@@@@@@.           ..            *@@@@@@@@@@@@=    -@@@@@@@@@@@:      
      -@@@@@@@@@@@@%     +@@@@@@@@@@=       :+#@@@@@%#+:        %@@@@@@@@@@=    -@@@@@@@@@@@@%      
      #@@@@@@@@@@@@@*     :%@@@@@@%-     -#@@@@@@@@@@@@@@#-      +@@@@@@@*.     @@@@@@@@@@@@@@-     
      @@@@@@@@@@@@@@@.      :=**+:     =@@@@@@@@@@@@@@@@@@@@+.     -=+=-       +@@@@@@@@@@@@@@+     
      @@@@@@@@@@@@@@@-               +@@@@@@@@@@@@@@@@@@@@@@@@*.               #@@@@@@@@@@@@@@+     
      #@@@@@@@@@@@@@@=             =@@@@@@@@@@@@@@@@@@@@@@@@@@@@+.             %@@@@@@@@@@@@@@-     
      :@@@@@@@@@@@@@@:           =%@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@+.           *@@@@@@@@@@@@@%      
       +@@@@@@@@@@@@#         .+@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@*.         .@@@@@@@@@@@@@:      
        =@@@@@@@@@@%.       =#@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@#=        -@@@@@@@@@@%:       
         :#@@@@@@@#     :=#@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@#=.     -@@@@@@@@+         
           .-+*+=.  :=#@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@%+-    -+**+-           
                 -*@@@@@@@@@@@@@@@@@@@@@@@@%#*#@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@#=.                 
              -*@@@@@@%#*+#*+=-%@@@@@@%#%.    *@@@@@@@##@@@%*+==@@@@@@@@@@%#****+*@%=.              
            -%@@@@@@-         =@@@*:          +@@+-.    +.     *@@@%#*+=-.      :@@@@@+             
          :%@@@@@@@*           @@%           .%@@             *@@@=            +@@@@@@@@-           
         :@@@@@@@@@      .:=+*%@@-      -*#%@@@#:            #@@@%-          .%@@@@@@@@@@+          
         %@@@@@@@@=    .@@@@@@@@#       @@%#@@@. .:--      .#@@@+            =@@@@@@@@@@@@-         
        -@@@@@@@@@     -*+=-:%@@:          .@@@@@@@+      *@@@@@.:=*%#      :@@@@@@@@@@@@@#         
        +@@@@@@@@=          :@@@@-.        .%@@@@@-      #@@@@@@@@@@%      +@@@@@@@@@@@@@@@         
        +@@@@@@@@:         :@@@@=       .:-*@@@@@:      =+=-:.:@@@@@.    .#@%#**#@@@@@@@@@@         
        +@@@@@@*       :-+*@@@@%     :%@@@@@@@@@%-            %@@@%.           :@@@@@@@@@@@         
        :@@@@@@.      +@@@@@@@@=     .@@@@@@@@@+           .+%@@@:             %@@@@@@@@@@#         
         #@@@@#      .@@@@@@@@@      *@@@@@@@@-            .@@@@:      .      -@@@@@@@@@@@-         
         .@@@@:  .   *@@@@@@@@+   .:-@@@@@@@@-      =*#%%@@@@@@-  .-=+*%:-==+*@@@@@@@@@@@*          
          .%@@#%@@%@@@@@@@@@@@++*%@@@@@@@@@@#+**#%@@@@@@@@@@@@@%@@@@@@@@@@@@@@@@@@@@@@@@*           
            +@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@-            
             .#@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@%%@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@+              
               :#@@@@@@@@@@@@@@@@@@@@@@@@@@%*=:.    .:=*%@@@@@@@@@@@@@@@@@@@@@@@@@@+.               
                 .+%@@@@@@@@@@@@@@@@@%#+-.                .-+#@@@@@@@@@@@@@@@@@@*-                  
                    .-+#%@@@@@@%#+=:                           .-=*#%@@@@@%#*=.                     
*/

/// @custom:file ffzz_erc721a.sol
/// @author m0k <support@fomofuzz.io>
/// @custom:version 1.03
/// @custom:greetings to Cryptoon_Goonz * KillaBears * Azuki * Hashlips

pragma solidity ^0.8.7;

import "./ReentrancyGuard.sol";
import "./Ownable.sol";
import "./Strings.sol";

import "./ERC721A.sol";

contract FomoFuzz is ERC721A, Ownable, ReentrancyGuard {
    
    /// @notice This contract is a implementation of ERC721A standard and also it inherits the functionalities of Ownable and ReentrancyGuard.
    /// @param totalBears variable stores the maximum number of bears that can be minted for this contract
    /// @param freeBears after this ammount of mints the price starts stacking
    /// @param ownerBears the ammount of bears the owner keeps in his wallet
    /// @param regularPrice variable stores the regular price of the token
    /// @param status variable stores the status of the contract, 1 for disabled and 2 for public
    /// @param revealed variable stores the visibility of the token metadata, 0 for not revealed and 1 for revealed
    /// @param _baseLink - The base URI for the token metadata
    /// @param _hiddenLink - The URI for the hidden token metadata

    using Strings for uint256;

    uint256 public totalBears = 10000;
    uint256 public freeBears  = 1000;
    uint256 public ownerBears = 100;
    uint256 public maxBatch   = 25;
    uint256 public maxTokenCount = 50;
    uint256 public regularPrice = 0 ether;
    uint256 public status = 1; // 1 = disabled ; 2 = public
    uint256 public revealed = 0; // 0 = not revealed ; 1 = revealed
    string  private _baseLink;
    string  private _hiddenLink;


    constructor() ERC721A("FomoFuZZ", "FFZZ") {
        _baseLink = ""; // initialize the base link to an empty string
        _hiddenLink = ""; // initialize the hidden link to an empty string

        // call the initial mint function to mint tokens for the owner
        _safeMint(msg.sender, ownerBears);
    }

    /// @notice This function allows the contract owner to withdraw any remaining balance from the contract. It uses the "payable()" function to call the owner's address and transfer the balance. The "nonReentrant" modifier is used to prevent reentrancy attacks.
    /// @dev This function can only be called by the contract owner.

    function withdraw() public onlyOwner nonReentrant {
        require(address(this).balance > 0, "Contract has no balance to withdraw.");
        require(msg.sender == owner(), "Only the contract owner can withdraw funds.");

        // Safely transfer the balance of the contract to the owner
        address payable ownerAddress = payable(owner());
        uint256 balance = address(this).balance;
        (bool success,) = ownerAddress.call{value: balance}("");
        require(success, "Failed to transfer funds to owner.");
    }

    /// @notice It sets the status variable to the given int_ value.
    /// @dev This function can only be called by the contract owner.
    /// @param int_ - The new status of the contract

    function setStatus(uint256 int_) external onlyOwner {
        status = int_;
    }

    /// @notice It sets the revealed variable to the given int_ value.
    /// @dev This function can only be called by the contract owner.
    /// @param int_ - set the status of the revealed variable

    function setRevealed(uint256 int_) external onlyOwner {
        revealed = int_;
    }


    /// @notice The setBaseTokenUri function sets the base URI for the token.
    /// @dev This function can only be called by the contract owner.
    /// @param string_ The new base URI for the token.

    function setBaseTokenUri(string calldata string_) external onlyOwner {
        _baseLink = string_;
    }

    /// @notice Allows the contract owner to set the hidden uri for the token.
    /// @dev This function can only be called by the contract owner.
    /// @param string_ The hidden uri to be set.

    function setHiddenUri(string calldata string_) external onlyOwner {
        _hiddenLink = string_;
    }

    /// @notice This function calculates the last price for the token based on the total supply. If the totalSupply is greater than 1100, the regularPrice is set to (totalSupply() * 0.0095 ether) / 1000.
    /// @dev This function is private and can only be accessed within the contract

    function wasLetztePreis() private {
        if (totalSupply() > ownerBears+freeBears) {
            uint256 multiplicator = 0.0095 ether;
            regularPrice = (totalSupply() * multiplicator) / 1000;
        }
    }

    /// @notice This function allows users to mint new bears. They need to provide enough ether to pay for the bears and the function checks various conditions such as if the user is the contract owner, if the maximum number of bears has been reached, if the batch size is valid, etc. before minting the bears.
    /// @param  howMuch - The number of bears to mint
    /// @custom:throws If user does not have enough Ethereum.

    function mintMyBearsBitch(uint256 howMuch)
        public
        payable
        mintEnabled
        greaterZero(howMuch)
        reachedMaxAmount
        areThereEnoughBears(howMuch)
        isTheBatchValid(howMuch)
        checkTokenCount
        callerIsUser
        nonReentrant
    {
        require(
            msg.value >= regularPrice * howMuch,
            "You don't have enough moolah, Bruh!"
        );
        _safeMint(msg.sender, howMuch);
        wasLetztePreis();
    }

    /// @notice This internal, view and virtual function returns the baseURI of the token. It is an override of the ERC721 function.
    /// @return memory - returns the baseURI of the token

    function _baseURI() internal view virtual override returns (string memory) {
        return _baseLink;
    }

    function contractURI() public pure returns (string memory) {
        return "https://fomofuzz.io/OPENSEA_DATA/contract_data.json";
    }
    
    /// @notice Returns the token URI for the given token ID. If the token is not revealed, it returns the hidden URI. Otherwise, it returns the base URI concatenated with the token ID and ".json".
    /// @param tokenId_ - The ID of the token to get the URI for
    /// @return {string} - The token URI
    /// @custom:throws Require assertion if the token does not exist

    function tokenURI(uint256 tokenId_)
        public
        view
        override
        returns (string memory)
    {
        require(_exists(tokenId_), "Token does not exist!");
        if (revealed == 0) {
            return _hiddenLink;
        } else {
            return
                string(
                    abi.encodePacked(
                        _baseLink,
                        Strings.toString(tokenId_),
                        ".json"
                    )
                );
        }
    }

    /// @notice Ensures that the call to the function is made by the user and not by a contract
    /// @custom:throws if the call is made by a contract rather than a user

    modifier callerIsUser() {
        require(tx.origin == msg.sender, "Go away !");
        _;
    }

    /// @notice Ensures that the number of bears being minted is greater than zero
    /// @param howMuch - the number of bears being minted
    /// @custom:throws if the number of bears being minted is zero or less

    modifier greaterZero(uint256 howMuch) {
        require(howMuch > 0, "Zero Bears might be a bad trade, Honey!");
        _;
    }

    /// @notice Ensures that the minting of bears is enabled
    /// @custom:throws if the minting of bears is not enabled

    modifier mintEnabled() {
        //
        require(status != 1, "Be patient! The bears are on their way!");
        _;
    }

    /// @notice Ensures that the number of bears being minted is less than the maximum batch size
    /// @param howMuch - the number of bears being minted
    /// @custom:throws if the number of bears being minted is greater than or equal to the maximum batch size

    modifier isTheBatchValid(uint256 howMuch) {
        require(
            howMuch < maxBatch+1,
            "No, bruh! You tried to mint too many bears at once"
        );
        _;
    }

    /// @notice Ensures that there are enough bears left to mint the requested amount
    /// @param howMuch - the number of bears being minted
    /// @custom:throws if there are not enough bears left to mint the requested amount

    modifier areThereEnoughBears(uint256 howMuch) {
        require(
            (totalSupply() + howMuch) < totalBears+1,
            "There arent that many bears left. Sorry!"
        );
        _;
    }

    /// @notice Ensures that the total minted amount does not exceed the maximum amount of bears that can be minted
    /// @custom:throws if the total minted amount exceeds the maximum amount of bears that can be minted

    modifier reachedMaxAmount() {
        require(
            totalSupply() < totalBears+1,
            "Mint amount will exceed total collection amount."
        );
        _;
    }

    /// @notice Ensures that the user does not own more bears than the maximum allowed
    /// @custom:throws if the user owns more bears than the maximum allowed
    modifier checkTokenCount() {
        require(
            balanceOf(msg.sender) < maxTokenCount,
            "You cant own more Bears!"
        );
        _;
    }
}

