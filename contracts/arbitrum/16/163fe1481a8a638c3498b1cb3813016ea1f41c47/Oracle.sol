// SPDX-License-Identifier: MIT

pragma solidity ^0.8.0;

import "./SafeMath.sol";

import "./Babylonian.sol";
import "./FixedPoint.sol";
import "./UniswapV2OracleLibrary.sol";
import "./Epoch.sol";
import "./IUniswapV2Pair.sol";
import "./ChainlinkInterface.sol";

// fixed window oracle that recomputes the average price for the entire period once every period
// note that the price average is only guaranteed to be over at least 1 period, but may be over a longer period
contract Oracle is Epoch {
    using FixedPoint for *;
    using SafeMath for uint256;

    address public constant WETH = 0x82aF49447D8a07e3bd95BD0d56f35241523fBab1;
    AggregatorV3Interface public feed = AggregatorV3Interface(0x639Fe6ab55C921f74e7fac1ee960C0B6293ba612);
    /* ========== STATE VARIABLES ========== */
    uint256 public PERIOD = 21600; // 6 hour TWAP (time-weighted average price)
    uint256 public CONSULT_LENIENCY = 120; // Used for being able to consult past the period end
    bool public ALLOW_STALE_CONSULTS = false; // If false, consult() will fail if the TWAP is stale
    // uniswap
    address public token0;
    address public token1;
    IUniswapV2Pair public pair;

    // oracle
    uint32 public blockTimestampLast;
    uint256 public price0CumulativeLast;
    uint256 public price1CumulativeLast;
    FixedPoint.uq112x112 public price0Average;
    FixedPoint.uq112x112 public price1Average;

    /* ========== CONSTRUCTOR ========== */

    constructor(IUniswapV2Pair _pair, uint256 _period, uint256 _startTime) Epoch(_period, _startTime, 0) {
        pair = _pair;
        token0 = pair.token0();
        token1 = pair.token1();
        price0CumulativeLast = pair.price0CumulativeLast(); // fetch the current accumulated price value (1 / 0)
        price1CumulativeLast = pair.price1CumulativeLast(); // fetch the current accumulated price value (0 / 1)
        uint112 reserve0;
        uint112 reserve1;
        (reserve0, reserve1, blockTimestampLast) = pair.getReserves();
        require(reserve0 != 0 && reserve1 != 0, "Oracle: NO_RESERVES"); // ensure that there's liquidity in the pair
    }

    function _getWethPriceInUsd() internal view returns (uint256) {
        uint256 price = uint256(feed.latestAnswer());
        return price.mul(1e10);
    }

    function setNewPeriod(uint256 _period) external onlyOperator {
        this.setPeriod(_period);
    }

    function setConsultLeniency(uint256 _consult_leniency) external onlyOperator {
        CONSULT_LENIENCY = _consult_leniency;
    }

    function setAllowStaleConsults(bool _allow_stale_consults) external onlyOperator {
        ALLOW_STALE_CONSULTS = _allow_stale_consults;
    }

    function canUpdate() public view returns (bool) {
        uint32 blockTimestamp = UniswapV2OracleLibrary.currentBlockTimestamp();
        uint32 timeElapsed = blockTimestamp - blockTimestampLast; // Overflow is desired
        return (timeElapsed >= PERIOD);
    }

    /* ========== MUTABLE FUNCTIONS ========== */

    /**
     * @dev Updates 1-day EMA price from Uniswap.
     */
    function update() external checkEpoch {
        (uint256 price0Cumulative, uint256 price1Cumulative, uint32 blockTimestamp) =
            UniswapV2OracleLibrary.currentCumulativePrices(address(pair));

        uint32 timeElapsed;
        unchecked {
            timeElapsed = blockTimestamp - blockTimestampLast; // overflow is desired
        }

        // Ensure that at least one full period has passed since the last update
        require(timeElapsed >= PERIOD, " PERIOD_NOT_ELAPSED");

        if (timeElapsed == 0) {
            // prevent divided by zero
            return;
        }

        // overflow is desired, casting never truncates
        // cumulative price is in (uq112x112 price * seconds) units so we simply wrap it after division by time elapsed
        unchecked {
            price0Average = FixedPoint.uq112x112(uint224((price0Cumulative - price0CumulativeLast) / timeElapsed));
            price1Average = FixedPoint.uq112x112(uint224((price1Cumulative - price1CumulativeLast) / timeElapsed));
        }

        price0CumulativeLast = price0Cumulative;
        price1CumulativeLast = price1Cumulative;
        blockTimestampLast = blockTimestamp;

        emit Updated(price0Cumulative, price1Cumulative);
    }

    // note this will always return 0 before update has been called successfully for the first time.
    function consult(address _token, uint256 _amountIn) external view returns (uint144 amountOut) {
        uint32 blockTimestamp = UniswapV2OracleLibrary.currentBlockTimestamp();
        uint32 timeElapsed;

        unchecked {
            timeElapsed = blockTimestamp - blockTimestampLast; // Overflow is desired
        }

        // Ensure that the price is not stale
        require((timeElapsed < (PERIOD + CONSULT_LENIENCY)) || ALLOW_STALE_CONSULTS, "STALE_PRICE_NEED_TO_CALL_UPDATE");

        if (_token == token0) {
            amountOut = price0Average.mul(_amountIn).decode144();

            if (token1 == WETH) {
                amountOut = uint144(uint256(amountOut).mul(_getWethPriceInUsd()).div(1e18));
            }
        } else {
            require(_token == token1, "Oracle: INVALID_TOKEN");
            amountOut = price1Average.mul(_amountIn).decode144();
            if (token0 == WETH) {
                amountOut = uint144(uint256(amountOut).mul(_getWethPriceInUsd()).div(1e18));
            }
        }
    }

    function twap(address _token, uint256 _amountIn) external view returns (uint144 _amountOut) {
        (uint256 price0Cumulative, uint256 price1Cumulative, uint32 blockTimestamp) =
            UniswapV2OracleLibrary.currentCumulativePrices(address(pair));
        uint32 timeElapsed = blockTimestamp - blockTimestampLast; // overflow is desired

        // Ensure that the price is not stale
        require((timeElapsed < (PERIOD + CONSULT_LENIENCY)) || ALLOW_STALE_CONSULTS, " STALE_PRICE_NEED_TO_CALL_UPDATE");

        if (_token == token0) {
            _amountOut = FixedPoint.uq112x112(uint224((price0Cumulative - price0CumulativeLast) / timeElapsed)).mul(
                _amountIn
            ).decode144();

            if (token1 == WETH) {
                _amountOut = uint144(uint256(_amountOut).mul(_getWethPriceInUsd()).div(1e18));
            }
        } else if (_token == token1) {
            _amountOut = FixedPoint.uq112x112(uint224((price1Cumulative - price1CumulativeLast) / timeElapsed)).mul(
                _amountIn
            ).decode144();

            if (token0 == WETH) {
                _amountOut = uint144(uint256(_amountOut).mul(_getWethPriceInUsd()).div(1e18));
            }
        }
    }

    event Updated(uint256 price0CumulativeLast, uint256 price1CumulativeLast);
}

